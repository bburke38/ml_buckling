# make a stiffened panel geometry, Sean P. Engelstad

cfgpmtr end_rib 0
cfgpmtr stiff_base 0

despmtr a 0.1
despmtr b 0.1
despmtr num_stiff 1
despmtr w_b 0.02
despmtr h_w 0.01

set num_local num_stiff+1
set s_p b/num_local

set y_start 0
patbeg ilocal num_local
   ifthen stiff_base EQ 1
      set b_local (b-num_stiff*w_b)/num_local
   else
      set b_local b/num_local
   endif
   
   ifthen ilocal GT 1 AND stiff_base EQ 1
      set y_start y_start+w_b
   endif
   
   # make the local panel section
   box 0 y_start 0 a b_local 0
   select face
    attribute capsGroup $panel
   ifthen ilocal GT 1
      union
   endif
   
   ifthen ilocal LT num_local
      
      # make the local stiffener base section
      set y_start y_start+b_local
      ifthen stiff_base EQ 1
         box 0 y_start 0 a w_b 0
         select face
         attribute capsGroup $base
         union
         set y_mid y_start+w_b*0.5
      else
         set y_mid y_start
      endif
   
      # make the stiffener wall      
      box 0 y_mid 0 a 0 -h_w
      select face
       attribute capsGroup $stiff
      select node 1
      select add 2
       attribute capsConstraint $stCorner #stiffener corners
      select edge 2
      select add 4
       attribute capsConstraint $stCorner #stiffener corners
      union
   
   endif
   
patend

ifthen end_rib EQ 1
   # ribs on each end
   box 0 0 0 0 b -h_w
   select face
   attribute capsGroup $rib
   union

   box a 0 0 0 b -h_w
   select face
   attribute capsGroup $rib
   union
endif

select body
attribute capsAIM $egadsTessAIM;tacsAIM   

end
||||||||||||||||||||